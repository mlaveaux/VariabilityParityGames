-----------------------------------------------------
-- Call Forward on No Reply
-- for reduced-phone.smv
-- only phone1 gets CFNR
-----------------------------------------------------

FEATURE cfnr-1
REQUIRE
   MODULE phone1(X,B,C,D,p)
   VAR
     st      : {idle,trying,ringing};
   MODULE phone2(X,B,C,D,p)
   VAR
     dialled : {0,1,2,3,4};
     st      : {trying,ringingt};
   MODULE ophone(X,B,C,D,p)
   VAR
     dialled : {0,1,2,3,4};
     st      : {trying,ringingt};

INTRODUCE
   MODULE phone1
   VAR forw : {0,2,4};
       nr : boolean;
   ASSIGN
     next(forw):=forw;
     init(nr) := 0;

   MODULE phone2
   VAR nr : boolean;
   ASSIGN
     init(nr) := 0;
     next(nr) := case
                   st=ringingt & next(st)=ringingt : 1;
                   st=ringingt : nr;
                   1 : 0;
                 esac;

   MODULE ophone
   VAR nr : boolean;
   ASSIGN
     init(nr) := 0;
     next(nr) := case
                   st=ringingt & next(st)=ringingt : 1;
                   st=ringingt : nr;
                   1 : 0;
                 esac;

CHANGE
   MODULE phone1  -- don't pick up the phone when CFNR takes over
     IF !forw=0 & st=ringing &
       ( (p[2].st=ringingt & p[2].dialled=1 & p[2].nr)
       | (p[3].st=ringingt & p[3].dialled=1 & p[3].nr))
        THEN IMPOSE next(st) := idle;

   MODULE phone2  -- if phone1 has CFNR and doesn't answer, divert call
     IF st=ringingt & nr & dialled=1 & !p[1].forw=0
        THEN IMPOSE next(dialled) := p[1].forw;
                    next(st) := trying;

   MODULE ophone  -- if phone1 has CFNR and doesn't answer, divert call
     IF st=ringingt & nr & dialled=1 & !p[1].forw=0
        THEN IMPOSE next(dialled) := p[1].forw;
                    next(st) := trying;
END
