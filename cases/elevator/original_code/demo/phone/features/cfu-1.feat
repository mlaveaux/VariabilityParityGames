-----------------------------------------------------
-- Call Forward Unconditional
-- for reduced-phone.smv
-- only phone1 gets CFU
-----------------------------------------------------

FEATURE cfu-1
REQUIRE
   MODULE phone1(X,B,C,D,p)
   VAR
     dialled : {0,1,2,3,4};
     st      : {idle};
   MODULE phone2(X,B,C,D,p)
   VAR
     dialled : {0,1,2,3,4};
     st      : {trying};
   MODULE ophone(X,B,C,D,p)
   VAR
     dialled : {0,1,2,3,4};
     st      : {trying};

INTRODUCE
   MODULE phone1
   VAR cfu-forw : {0,2,4};
   ASSIGN
     next(cfu-forw):=cfu-forw;

CHANGE
   MODULE phone1  -- if we forward calls, we don't go from idle to ringing
     IF !cfu-forw=0 & st=idle &
       ( (p[2].st=trying & p[2].dialled=X & !next(p[2].dialled)=1)
       | (p[3].st=trying & p[3].dialled=X & !next(p[3].dialled)=1))
        THEN IMPOSE next(st) := idle;

   MODULE phone2  -- if we try someone with CFU, we are forwarded 
     IF st=trying & dialled=1 & !p[1].cfu-forw=0
        THEN IMPOSE next(dialled) := p[1].cfu-forw; next(st):=trying;

   MODULE ophone  -- if we try someone with CFU, we are forwarded 
     IF st=trying & dialled=1 & !p[1].cfu-forw=0
        THEN IMPOSE next(dialled) := p[1].cfu-forw; next(st):=trying;
END

-------
