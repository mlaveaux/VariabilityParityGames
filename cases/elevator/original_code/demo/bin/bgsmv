#! tcsh

# help

if ( $# <= 1 || $1 == "-h" || $1 == "--help" ) then
  echo "usage:"
  echo "  bgsmv outfile [smv-options] smv-file"
  echo 'Use "&" to put smv into the background!'
  exit
endif

# get filename for output
set filename = $1
set index = $#
set smvfile = $argv[$index]
shift

if (! -e $smvfile) then
   echo "bgsmv: SMV input file does not exist"
   exit
endif

# make filename unique
if (-e $filename) then
   while (-e $filename) 
	 set filename = $filename".x"
   end
   echo "bgsmv: output file existed, name changed to $filename"
endif

# put some additional information in output file
echo "command line:  smv $*"     >& $filename
echo "  on host:  $HOST"         >>& $filename
echo "  output file:  $filename" >>& $filename
date			         >>& $filename
echo "======================"    >>& $filename
echo "verifying:"	         >>& $filename
awk '/^[ \t]*SPEC/,/(^[ \t]*$)|(^[ \t]*--+)/' $smvfile >>& $filename
echo "======================"    >>& $filename
# echo "fairness:"	         >>& $filename
# awk '/^[ \t]*FAIRNESS/,/(^[ \t]*$)|(^[ \t]*--+)/' $smvfile >>& $filename
# echo "======================"    >>& $filename

# start smv
nice +19 smv $*		         >>& $filename

echo "======================"    >>& $filename
date                             >>& $filename
