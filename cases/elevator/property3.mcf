% property 3
% AG forall i < j. (Level = i & lifBut_j.pressed & direction = up -> A[direction=up U Level = j])
% &
% AG forall i > j. (Level = i & lifBut_j.pressed & direction = down -> A[direction=down U Level = j])
%
% invariantly, if the lift is travelling up (down) while there are calls in that direction it will not change the direction it is travelling
%
% NB: since we (currently) don't have a way to test on which Level we are,
% other than through opening the door, we resort to using that action...
% and we only check that we're moving up (down)

[true*]
  (
    (
      [direction(up).(! (direction(down) || exists k:Floor. open(k)))*] 
        forall i:Floor. 
          val(1 <= i && i <= 5) => 
            [open(i)]
              forall j:Floor. 
                 val(i < j && j <= 5) => 
                   [liftButton(j)]
                     mu Y. ( [!open(j)]Y && [direction(down)]false && <true>true )
    )
  &&

    (
      [direction(down).(! (direction(up) || exists k:Floor. open(k)))*] 
        forall i:Floor. 
          val(1 <= i && i <= 5) => 
            [open(i)]
              forall j:Floor. 
                 val(5 >= i && i > j ) => 
                   [liftButton(j)]
                     mu Y. ( [!open(j)]Y && [direction(up)]false && <true>true )
    )

  )
